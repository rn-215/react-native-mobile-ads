import io.invertase.gradle.common.PackageJson

buildscript {
  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath("com.android.tools.build:gradle:7.0.1")
    }
  }
}

plugins {
  id "io.invertase.gradle.build" version "1.5"
}

def packageJson = PackageJson.getForProject(project)
def admobVersion = packageJson['sdkVersions']['android']['admob']
def jsonMinSdk = packageJson['sdkVersions']['android']['minSdk']
def jsonTargetSdk = packageJson['sdkVersions']['android']['targetSdk']
def jsonCompileSdk = packageJson['sdkVersions']['android']['compileSdk']
def jsonBuildTools = packageJson['sdkVersions']['android']['buildTools']

project.ext {
  set('react-native', [
    versions: [
      android : [
        minSdk    : jsonMinSdk,
        targetSdk : jsonTargetSdk,
        compileSdk: jsonCompileSdk,
        // optional as gradle.buildTools comes with one by default
        // overriding here though to match the version RN uses
        buildTools: jsonBuildTools
      ],

      admob: [
        sdk: admobVersion,
      ],

      ads     : [
        consent: "1.0.6"
      ],
    ],
  ])
}

apply from: file("./admob-json.gradle")

def admobJSONAdmobAppIDString = ""
def admobJSONAdmobDelayAppMeasurementInitBool = false

if (rootProject.ext.admobJson) {
  admobJSONAdmobAppIDString = rootProject.ext.admobJson.getStringValue("android_app_id", "")
  admobJSONAdmobDelayAppMeasurementInitBool = rootProject.ext.admobJson.isFlagEnabled("delay_app_measurement_init", false)
}

if (!admobJSONAdmobAppIDString) {
  // todo throw a build error?
}

android {
  defaultConfig {
    multiDexEnabled true
    manifestPlaceholders = [
      admobJSONAdmobAppID                  : admobJSONAdmobAppIDString,
      admobJSONAdmobDelayAppMeasurementInit: admobJSONAdmobDelayAppMeasurementInitBool
    ]
  }
  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation("com.google.android.gms:play-services-ads:19.8.0") { force = true; }
  implementation "com.google.android.ads.consent:consent-library:${ReactNative.ext.getVersion("ads", "consent")}"
}

ReactNative.shared.applyPackageVersion()
ReactNative.shared.applyDefaultExcludes()
ReactNative.module.applyAndroidVersions()
ReactNative.module.applyReactNativeDependency("api")
